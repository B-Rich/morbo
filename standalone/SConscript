# -*- Mode: Python -*-

Import('freestanding_env')

fenv = freestanding_env.Clone()

fenv['CPPPATH'] = ["#include", "include/"]
fenv['LINKFLAGS'] += " -T standalone/morbo.ld" 

cutil = fenv.StaticLibrary('cutil',
                           [ 'strncmp.c',
                             'strncpy.c',
                             'strtok.c',
                             'strtoul.c',
                             ])

AlwaysBuild(Command('version.inc', [], """git describe | sed 's/^\\(.*\\)$/"\\1"/' > $TARGET"""))

morbo = fenv.Program('morbo.debug',
                     ['start.asm',
                      'crc16.c',
                      'util.c',
                      'elf.c',
                      'reboot.c',
                      'pci.c',
                      'pci_db.c',
                      'printf.c',
                      'morbo.c',
                      'version.c',
                      'serial.c',
                      'ohci.c',
                      ],
                     LIBS=['cutil'],
                     LIBPATH=['.']
                     )

morbo = Command('morbo', morbo, 'strip -o $TARGET $SOURCE')

Depends(morbo, 'morbo.ld')

Install('#tftp', [ morbo ])


rom_env = freestanding_env.Clone()

rom_env['CPPPATH'] = ["#include", "include/"]
rom_env['CFLAGS'] += "-fomit-frame-pointer -Os"
rom_env['LINKFLAGS'] += " -T standalone/option-rom.ld" 

rom = rom_env.Program('option-rom.bin-unfixed',
                      ['option-rom.asm',
                       'or-main.c'])

rom_fixed = fenv.Command('option-rom.bin', ['rom_fixup.py', rom],
                         "${SOURCES[0]} ${SOURCES[1:]} $TARGET")



# EOF
