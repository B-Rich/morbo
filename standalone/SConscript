# -*- Mode: Python -*-

Import('freestanding_env')

fenv = freestanding_env.Clone()

fenv['CPPPATH'] = ["#include", "include/"]
fenv['LINKFLAGS'] += " -T standalone/morbo.ld" 
fenv['LIBPATH'] = ['.']

stand = fenv.StaticLibrary('stand',
                           [ 'elf.c',
                             'hexdump.c',
                             'mbi.c',
                             'pci.c',
                             'pci_db.c',
                             'printf.c',
                             'realmode.asm',
                             'reboot.c',
                             'serial.c',
                             'start.asm',
                             'util.c',
                             'version.c',

                             # libc stuff
                             'memcpy.c',
                             'memcmp.c',
                             'memset.c',
                             'strlen.c',

                             # FreeBSD libc stuff
                             'strncmp.c',
                             'strncpy.c',
                             'strtok.c',
                             'strtoull.c',
                             'qdivrem.c',
                             ])

tinf = fenv.StaticLibrary('tinf',
                          [ 'tinflate.c',
                            'tinfgzip.c',
                            'tinfzlib.c',
                            'adler32.c',
                            'crc32.c',
                            ])

AlwaysBuild(Command('version.inc', [], """git describe --dirty | sed 's/^\\(.*\\)$/"\\1"/' > $TARGET"""))

def DoInstall(prog, compress=True):
    Depends(prog, 'morbo.ld')
    prog = fenv.Strip(prog)
    if compress:
        prog = fenv.GZip(prog)
    Install('#tftp', prog)

# Morbo

DoInstall(fenv.Program('morbo.debug',
                       [ 'crc16.c',
                         'morbo.c',
                         'ohci.c' ],
                       LIBS=['stand', 'tinf']))

# Zapp

DoInstall(fenv.Program('zapp.debug',
                       [ 'acpi.c',
                         'zapp.c',
                         ],
                       LIBS=['stand', 'tinf']))

# VILO

DoInstall(fenv.Program('vilo.debug',
                       [ 'vilo.c' ],
                       LIBS=['stand', 'tinf']))

# Bender

DoInstall(fenv.Program('bender.debug',
                       [ 'bender.c',
                         ],
                       LIBS=['stand', 'tinf']))

# Unzip

DoInstall(fenv.Program('unzip.debug',
                       [ 'unzip.c',
                         ],
                       LIBS=['stand', 'tinf']),
          compress=False)

# Farnsworth

DoInstall(fenv.Program('farnsworth.debug',
                       [ 'farnsworth.c',
                         ],
                       LIBS=['stand', 'tinf']))

# Incomplete version of Option ROM

rom_env = freestanding_env.Clone()

rom_env['CPPPATH'] = ["#include", "include/"]
rom_env['CFLAGS'] += "-fomit-frame-pointer -Os"
rom_env['LINKFLAGS'] += " -T standalone/option-rom.ld" 

rom = rom_env.Program('option-rom.bin-unfixed',
                      ['option-rom.asm',
                       'or-main.c'])

rom_fixed = fenv.Command('option-rom.bin', ['rom_fixup.py', rom],
                         "${SOURCES[0]} ${SOURCES[1:]} $TARGET")

# EOF
